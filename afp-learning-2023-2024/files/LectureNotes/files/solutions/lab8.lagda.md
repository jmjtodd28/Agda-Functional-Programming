# Week 8 - Lab Sheet

```agda
{-# OPTIONS --safe --without-K --auto-inline #-}

module solutions.lab8 where

open import prelude
open import decidability
open import Fin
open import List-functions
open import isomorphisms
open import sorting
open import strict-total-order
```

In this lab sheet you will practice with **strict total orders** and **positions**.

## Exercise 1 - The Lexicographical Order on `â„• Ã— â„•`

The *lexicographical order* on `â„•Â²` is defined as: `(n , m) < (n' , m')`
precisely when (`n < n'`) or (`n = n'` and `m < m'`).

So, for example, `(3 , 19) < (5, 2)` and `(7 , 3) < (7 , 4)`, but `Â¬ (11 , 4) <
(11 , 1)` and `Â¬ (5 , 1) < (4 , 3)`.

We are going to prove that the lexicographical order is a strict total order on
`â„•Â²`.

### Exercise 1.1

**Translate** the definition of the lexicographical order to Agda.

```agda
â„•Â² : Type
â„•Â² = â„• Ã— â„•

_<â‚—_ : â„•Â² â†’ â„•Â² â†’ Type
(n , m) <â‚— (n' , m') = (n <â‚™ n') âˆ” ((n â‰¡ n') Ã— (m <â‚™ m'))
```

### Exercise 1.2

**Prove** that the lexicographical order is irreflexive.

```agda
<â‚—-is-irreflexive : (p : â„•Â²) â†’ Â¬ (p <â‚— p)
<â‚—-is-irreflexive (n , m) (inl n<n)       = <â‚™-irreflexive n n<n
<â‚—-is-irreflexive (n , m) (inr (_ , m<m)) = <â‚™-irreflexive m m<m
```

### Exercise 1.3

**Prove** that the lexicographical order is transitive.

```agda
<â‚—-is-transitive : {p q r : â„•Â²} â†’ p <â‚— q â†’ q <â‚— r â†’ p <â‚— r
<â‚—-is-transitive {n , m} {n' , m'} {n'' , m''}
                 (inl n<n') (inl n'<n'') = inl (<â‚™-trans n<n' n'<n'')
<â‚—-is-transitive {n , m} {n' , m'} {n'' , m''}
                 (inl n<n') (inr (refl n' , _)) = inl n<n'
<â‚—-is-transitive {n , m} {.n , m'} {n'' , m''}
                 (inr (refl n , m<m')) (inl n<n'') = inl n<n''
<â‚—-is-transitive {n , m} {.n , m'} {n'' , m''}
                 (inr (refl n , m<m')) (inr (refl n , m'<m'')) = goal
 where
  goal : (n <â‚™ n) âˆ” ((n â‰¡ n) Ã— (m <â‚™ m''))
  goal = inr (refl n , <â‚™-trans m<m' m'<m'')
```

### Exercise 1.4

**Prove** that the lexicographical is connected.

```agda
<â‚—-is-connected : {p q : â„•Â²} â†’ Â¬ (p â‰¡ q) â†’ (p <â‚— q) âˆ” (q <â‚— p)
<â‚—-is-connected {n , m} {n' , m'} pairs-not-equal =
 lemma (â„•-has-decidable-equality n n' , â„•-has-decidable-equality m m')
 where
  lemma : (n â‰¡ n') âˆ” Â¬ (n â‰¡ n')
        Ã— (m â‰¡ m') âˆ” Â¬ (m â‰¡ m')
        â†’ ((n , m) <â‚— (n' , m')) âˆ” ((n' , m') <â‚— (n , m))
  lemma (inl (refl n) , inl (refl m)) = ðŸ˜-elim (pairs-not-equal (refl (n , m)))
  lemma (inl e , inr ne') = sublemma (<â‚™-connected ne')
   where
    sublemma : (m <â‚™ m') âˆ” (m' <â‚™ m)
             â†’ ((n , m) <â‚— (n' , m')) âˆ” ((n' , m') <â‚— (n , m))
    sublemma (inl m<m') = inl (inr (e     , m<m'))
    sublemma (inr m'<m) = inr (inr (sym e , m'<m))
  lemma (inr ne , _)       = sublemma (<â‚™-connected ne)
   where
    sublemma : (n <â‚™ n') âˆ” (n' <â‚™ n)
             â†’ ((n , m) <â‚— (n' , m')) âˆ” ((n' , m') <â‚— (n , m))
    sublemma (inl n<n') = inl (inl n<n')
    sublemma (inr n'<n) = inr (inl n'<n)
```

### Exercise 1.5

We will also need to prove that `â„• Ã— â„•` has decidable equality.  Let's show
this more generally:

**Prove** that if `X` and `Y` have decidable equality, then so does their
cartesian product `X Ã— Y`.

```agda
Ã—-has-decidable-equality : {X Y : Type}
                         â†’ has-decidable-equality X
                         â†’ has-decidable-equality Y
                         â†’ has-decidable-equality (X Ã— Y)
Ã—-has-decidable-equality Î´X Î´Y (x , y) (x' , y') = lemma (Î´X x x' , Î´Y y y')
 where
  lemma : (x â‰¡ x') âˆ” Â¬ (x â‰¡ x')
        Ã— (y â‰¡ y') âˆ” Â¬ (y â‰¡ y')
        â†’ is-decidable ((x , y) â‰¡ (x' , y'))
  lemma (inl (refl x) , inl (refl y)) = inl (refl (x , y))
  lemma (inl (refl x) , inr ne'     ) = inr (Î» e â†’ ne' (ap snd e))
  lemma (inr ne       , inl (refl y)) = inr (Î» e â†’ ne  (ap fst e))
  lemma (inr ne       , inr _       ) = inr (Î» e â†’ ne  (ap fst e))
```

**Conclude** that `â„• Ã— â„•` has decidable equality.

```agda
â„•Â²-has-decidable-equality : has-decidable-equality â„•Â²
â„•Â²-has-decidable-equality =
 Ã—-has-decidable-equality â„•-has-decidable-equality â„•-has-decidable-equality
```

### Exercise 1.6

**Conclude** that the lexicographical order is a strict total order on `â„•Â²`.

```agda
strict-total-order-on-â„•Â² : StrictTotalOrder â„•Â²
strict-total-order-on-â„•Â² = record {
    _<_            = _<â‚—_
  ; â‰¡-is-decidable = â„•Â²-has-decidable-equality
  ; irreflexive    = <â‚—-is-irreflexive
  ; transitive     = <â‚—-is-transitive
  ; connected      = <â‚—-is-connected
  }
```

## Exercise 2 - Non-Strict Total Orders

In the lectures, a type of strict total order was introduced. Similarly, we can
also introduce a type of *non-strict total orders*.

For example, the type of natural numbers with `â‰¤` is an example of a non-strict
total order.

```agda
record NonStrictTotalOrder (X : Type) : Typeâ‚ where
 field
  _â‰¤_ : X â†’ X â†’ Type
  â‰¡-is-decidable : has-decidable-equality X
  reflexive : (x : X) â†’ x â‰¤ x
  transitive : {x y z : X} â†’ x â‰¤ y â†’ y â‰¤ z â†’ x â‰¤ z
  antisymmetric : {x y : X} â†’ x â‰¤ y â†’ y â‰¤ x â†’ x â‰¡ y
  strongly-connected : (x y : X) â†’ (x â‰¤ y) âˆ” (y â‰¤ x)
```

Now let's assume that we are given a *strict* total order. We make this
assumption using a module, which means it will be available to you in the code
below.

```agda
module _
        {X : Type}
        (sto : StrictTotalOrder X)
       where

 open StrictTotalOrder sto
```

Recall that in [strict-total-order](../strict-total-order.lagda.md#sorted-lists), we defined the order `â‰¤` from the strict total order `<`.

```agdacode
 _â‰¤_ : X â†’ X â†’ Type
 x â‰¤ y = (y â‰¡ x) âˆ” (x < y)
```

Notice how `â‰¤` was (implicitly) used in the definition of `Sorted` given in that file.

### Exercise 2.1

**Prove** the following facts about `â‰¤`.

```agda
 <-to-â‰¤ : {x y : X} â†’ x < y â†’ x â‰¤ y
 <-to-â‰¤ = inr

 â‰¤-is-reflexive : (x : X) â†’ x â‰¤ x
 â‰¤-is-reflexive x = inl (refl x)
```

### Exercise 2.2

Using transitivity of `<`, **prove** a lemma and that `â‰¤` is transitive.

```agda
 <-â‰¤-trans : {x y z : X} â†’ x < y â†’ y â‰¤ z â†’ x â‰¤ z
 <-â‰¤-trans x<y (inl (refl _)) = inr x<y
 <-â‰¤-trans x<y (inr y<z     ) = inr (transitive x<y y<z)

 â‰¤-is-transitive : {x y z : X} â†’ x â‰¤ y â†’ y â‰¤ z â†’ x â‰¤ z
 â‰¤-is-transitive (inl (refl _)) yâ‰¤z = yâ‰¤z
 â‰¤-is-transitive (inr x<y     ) yâ‰¤z = <-â‰¤-trans x<y yâ‰¤z
```

### Exercise 2.3

**Prove** antisymmetry of `â‰¤`.

```agda
 â‰¤-is-antisymmetric : {x y : X} â†’ x â‰¤ y â†’ y â‰¤ x â†’ x â‰¡ y
 â‰¤-is-antisymmetric (inl e  ) q         = sym e
 â‰¤-is-antisymmetric (inr x<y) (inl e)   = e
 â‰¤-is-antisymmetric (inr x<y) (inr y<x) = ðŸ˜-elim (antisymmetric _ _ x<y y<x)
```

### Exercise 2.4

**Show** that `â‰¤` is strongly connected.

```agda
 â‰¤-is-strongly-connected : (x y : X) â†’ (x â‰¤ y) âˆ” (y â‰¤ x)
 â‰¤-is-strongly-connected x y = lemma (â‰¡-is-decidable x y)
  where
   lemma : (x â‰¡ y) âˆ” Â¬ (x â‰¡ y)
         â†’ (x â‰¤ y) âˆ” (y â‰¤ x)
   lemma (inl e ) = inr (inl e)
   lemma (inr ne) = sublemma (connected ne)
    where
     sublemma : (x < y) âˆ” (y < x)
              â†’ (x â‰¤ y) âˆ” (y â‰¤ x)
     sublemma (inl x<y) = inl (<-to-â‰¤ x<y)
     sublemma (inr y<x) = inr (<-to-â‰¤ y<x)
```

### Exercise 2.5

Finally, **complete** the definition of the non-strict total order on `X`.

```agda
 non-strict-total-order-from-strict-total-order : NonStrictTotalOrder X
 non-strict-total-order-from-strict-total-order = record {
    _â‰¤_                = _â‰¤_
  ; â‰¡-is-decidable     = â‰¡-is-decidable
  ; reflexive          = â‰¤-is-reflexive
  ; transitive         = â‰¤-is-transitive
  ; antisymmetric      = â‰¤-is-antisymmetric
  ; strongly-connected = â‰¤-is-strongly-connected
  }
```

## Exercise 3 - More on Positions

Recall from the lectures that we defined a type of *positions* (or indices) for
a given list as follows.

```agdacode
Pos : {X : Type} â†’ List X â†’ Type
Pos        [] = ðŸ˜
Pos (_ :: xs) = ðŸ™ âˆ” Pos xs
```

We will consider an inductively defined version of here and prove it to be
isomorphic to `Pos`.

The inductive definition is as follows.

```agda
data Pos' {X : Type} : List X â†’ Type where
  here : {x : X} {xs : List X}
       â†’ Pos' (x :: xs)
  there : {x : X} {xs : List X}
        â†’ (p : Pos' xs) â†’ Pos' (x :: xs)
```

### Exercise 3.1

**Define** a function from `Pos xs` to `Pos' xs`.

```agda
Pos-to-Pos' : {X : Type} (xs : List X) â†’ Pos xs â†’ Pos' xs
Pos-to-Pos' []          = ðŸ˜-elim
Pos-to-Pos' (x :: xs) (inl â‹†) = here
Pos-to-Pos' (x :: xs) (inr p) = there (Pos-to-Pos' xs p)
```

### Exercise 3.2

**Define** a function the other way, i.e. from `Pos' xs` to `Pos xs`.

```agda
Pos'-to-Pos : {X : Type} (xs : List X) â†’ Pos' xs â†’ Pos xs
Pos'-to-Pos []        ()
Pos'-to-Pos (x :: xs) here      = inl â‹†
Pos'-to-Pos (x :: xs) (there p) = inr (Pos'-to-Pos xs p)
```

### Exercise 3.3

Using the above functions, **prove** that `Pos xs` is isomorphic to `Pos' xs`
for every list `xs`.

```agda
Pos-isomorphic-to-Pos' : {X : Type} (xs : List X) â†’ Pos xs â‰… Pos' xs
Pos-isomorphic-to-Pos' {X} xs = record { bijection = f ; bijectivity = f-is-bijection }
 where
  f : Pos xs â†’ Pos' xs
  f = Pos-to-Pos' xs

  g : Pos' xs â†’ Pos xs
  g = Pos'-to-Pos xs

  gf : g âˆ˜ f âˆ¼ id
  gf = lemma xs
   where
    lemma : (ys : List X)
          â†’ (Pos'-to-Pos ys âˆ˜ Pos-to-Pos' ys) âˆ¼ id
    lemma (y :: ys) (inl â‹†) = refl _
    lemma (y :: ys) (inr p) = ap inr (lemma ys p)

  fg : f âˆ˜ g âˆ¼ id
  fg = lemma xs
   where
    lemma : (ys : List X)
          â†’ (Pos-to-Pos' ys âˆ˜ Pos'-to-Pos ys) âˆ¼ id
    lemma (y :: ys) here      = refl _
    lemma (y :: ys) (there p) = ap there (lemma ys p)

  f-is-bijection : is-bijection f
  f-is-bijection = record { inverse = g ; Î· = gf ; Îµ = fg }
```

### Exercise 3.4

Yet another way to define the positions is by using `Fin (length xs)`, the
inductively defined type that has exactly `length xs`-many elements.

**Define** the map that takes a position of `xs` and gives an element of `Fin
  (length xs)`.

```agda
Pos-to-Fin-length : {X : Type} (xs : List X) â†’ Pos xs â†’ Fin (length xs)
Pos-to-Fin-length []                = ðŸ˜-elim
Pos-to-Fin-length (x :: xs) (inl â‹†) = zero
Pos-to-Fin-length (x :: xs) (inr p) = suc (Pos-to-Fin-length xs p)
```

### Exercise 3.5

**Define** the map in the other direction.

```agda
Fin-length-to-Pos : {X : Type} (xs : List X) â†’ Fin (length xs) â†’ Pos xs
Fin-length-to-Pos []        = Fin-0-is-empty
Fin-length-to-Pos (x :: xs) zero    = inl â‹†
Fin-length-to-Pos (x :: xs) (suc i) = inr (Fin-length-to-Pos xs i)
```

### Exercise 3.6

Using the above functions, **prove** that `Pos xs` is isomorphic to `Fin (length
xs)` for every list `xs`.

```agda
Pos-isomorphic-to-Fin-length : {X : Type} (xs : List X)
                             â†’ Pos xs â‰… Fin (length xs)
Pos-isomorphic-to-Fin-length {X} xs = record { bijection = f ; bijectivity = f-is-bijection }
 where
  f : Pos xs â†’ Fin (length xs)
  f = Pos-to-Fin-length xs

  g : Fin (length xs) â†’ Pos xs
  g = Fin-length-to-Pos xs

  gf : g âˆ˜ f âˆ¼ id
  gf = lemma xs
   where
    lemma : (ys : List X)
          â†’ (Fin-length-to-Pos ys âˆ˜ Pos-to-Fin-length ys) âˆ¼ id
    lemma (y :: ys) (inl â‹†) = refl _
    lemma (y :: ys) (inr p) = ap inr (lemma ys p)

  fg : f âˆ˜ g âˆ¼ id
  fg = lemma xs
   where
    lemma : (ys : List X)
          â†’ (Pos-to-Fin-length ys âˆ˜ Fin-length-to-Pos ys) âˆ¼ id
    lemma (y :: ys) zero    = refl _
    lemma (y :: ys) (suc i) = ap suc (lemma ys i)

  f-is-bijection : is-bijection f
  f-is-bijection = record { inverse = g ; Î· = gf ; Îµ = fg }
```
