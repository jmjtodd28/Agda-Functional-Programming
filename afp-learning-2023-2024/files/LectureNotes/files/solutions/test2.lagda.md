# Test 2

```agda
module solutions.test2 where

open import prelude
open import natural-numbers-type
open import natural-numbers-functions hiding (max)
open import Bool
open import List
open import List-functions
open import isomorphisms
open import strict-total-order
open import Fin
open import binary-type
open import decidability
open _â‰…_

open import solutions.lab2 using (max ; max-commutative)

```

## Important remarks
1. Put your student ID card on your desk.
1. You are not allowed to talk or use your phone during the test. If you do,
this will be considered an instance of plagiarism.
1. You can use a web-browser only for reading the lecture notes and the Agda
manual. If you use it for anything else, this will be considered an instance
of plagiarism.
1. Please do not ask questions to the invigilators unless you think there is a
mistake in the test.
1. You can do these questions in any order. In particular, if you cannot work
out the proof of something, you can leave the hole empty and still use it in
other questions and get full marks for those other questions.
1. Each of the five sections has equal weight (20% each).
1. Your answers will be marked on correctness *and* quality.
1. The test starts at 16:00. The test ends at 17:50, with 5% penalty for submissions at 17:59. No submissions are possible after 17:59, so make sure you submit before 18:00. Students with extra time will be able see their deadlines, with and without penalty, on Canvas.
1. You must not leave the room between 17:40 and 17:50 to avoid disruption.

## Please check your Canvas submission after you have submitted

This is necessary to make sure you submitted the file you intended to submit. Often students accidentally submit the **wrong file**.

It is also a good idea to submit to Canvas well before the deadline when you have completed enough questions. We will mark the latest submission.

## Question 1

Consider the type of binary trees.

```agda
data T (X : Type) : Type where
  lf : T X
  nd : X â†’ T X â†’ T X â†’ T X
```

**Task 1.1.** The `size` of a binary tree is the number of nodes it contains.  Implement this function:
```agda
size : {X : Type} â†’ T X â†’ â„•
size lf         = 0
size (nd _ l r) = 1 + size l + size r
```
Additionally, we have the pre-order traversal which produces the list of nodes from the tree:
```agda
flatten : {X : Type} â†’ T X â†’ List X
flatten lf = []
flatten (nd x l r) = x :: flatten l ++ flatten r
```
Recall that the length of the concatenation of two lists is the sum of their lengths:
```agda
length-++ : {X : Type} (xs ys : List X) â†’ length xs + length ys â‰¡ length (xs ++ ys)
length-++ [] ys = refl _
length-++ (x :: xs) ys = ap suc (length-++ xs ys)
```
**Task 1.2.** Show that the size of a tree is the same as the length of its flattening.
```agda
flatten-size : {X : Type} (t : T X) â†’ size t â‰¡ length (flatten t)
flatten-size lf         = refl zero
flatten-size (nd _ l r) =
 1 + size l + size r                         â‰¡âŸ¨ apâ‚‚ (Î» a b â†’ 1 + a + b) (flatten-size l) (flatten-size r) âŸ©
 1 + length (flatten l) + length (flatten r) â‰¡âŸ¨ ap suc (length-++ (flatten l) (flatten r)) âŸ©
 1 + length (flatten l ++ flatten r)         âˆ
```

**Hint.** A possible solution uses `apâ‚‚` and the provided lemma `length-++`.

## Question 2

**Task 2.1.** Define the height of a binary tree as the length of its longest branch.
```agda
height : {A : Type} â†’ T A â†’ â„•
height lf         = 0
height (nd _ l r) = 1 + max (height l) (height r)
```
Now recall that the `mirror` function is defined by recursively swapping the left and right branches:
```agda
mirror : {A : Type} â†’ T A â†’ T A
mirror lf = lf
mirror (nd x l r) = nd x (mirror r) (mirror l)
```
**Task 2.2.** Show that the height of a tree and its mirror are the same:
```agda
height-of-mirror : {A : Type} (t : T A) â†’ height (mirror t) â‰¡ height t
height-of-mirror lf         = refl zero
height-of-mirror (nd _ l r) =
 1 + max (height (mirror r)) (height (mirror l)) â‰¡âŸ¨ apâ‚‚ (Î» a b â†’ 1 + max a b) (height-of-mirror r) (height-of-mirror l) âŸ©
 1 + max (height r) (height l)                   â‰¡âŸ¨ ap suc (max-commutative (height r) (height l)) âŸ©
 1 + max (height l) (height r)                   âˆ
```

**Hint.** You may wish to use `max-commutative` and `apâ‚‚`.

## Question 3

We can define the type of positions in a binary tree as follows:

```agda
BinPos : {X : Type} â†’ T X â†’ Type
BinPos lf = ğŸ˜
BinPos (nd x l r) = ğŸ™ âˆ” BinPos l âˆ” BinPos r

fetch : {X : Type} (t : T X) â†’ BinPos t â†’ X
fetch (nd x l r) (inl â‹†) = x
fetch (nd x l r) (inr (inl lp)) = fetch l lp
fetch (nd x l r) (inr (inr rp)) = fetch r rp
```
**Task 3.1.** Show that every position of a tree gives rise to a position of its mirror:
```agda
mirror-pos : {X : Type} (t : T X) â†’ BinPos t â†’ BinPos (mirror t)
mirror-pos lf ()
mirror-pos (nd _ l r) (inl â‹†)       = inl â‹†
mirror-pos (nd _ l r) (inr (inl p)) = inr (inr (mirror-pos l p))
mirror-pos (nd _ l r) (inr (inr p)) = inr (inl (mirror-pos r p))
```
**Task 3.2.** Show that the element in a tree at a given position is the same as
the element in the mirrored tree at the mirrored position.
```
mirror-fetch : {X : Type} (t : T X) (p : BinPos t)
  â†’ fetch t p â‰¡ fetch (mirror t) (mirror-pos t p)
mirror-fetch lf ()
mirror-fetch (nd x l r) (inl â‹†)       = refl x
mirror-fetch (nd _ l r) (inr (inl p)) = mirror-fetch l p
mirror-fetch (nd _ l r) (inr (inr p)) = mirror-fetch r p
```

## Question 4

We define `foldr` and `foldl` as follows.
```agda
foldl : {X Y : Type} â†’ (Y â†’ X â†’ Y) â†’ Y â†’ List X â†’ Y
foldl f u []        = u
foldl f u (x :: xs) = f (foldl f u xs) x

foldr : {X Y : Type} â†’ (X â†’ Y â†’ Y) â†’ Y â†’ List X â†’ Y
foldr f u []        = u
foldr f u (x :: xs) = f x (foldr f u xs)
```
The following two examples illustrate the different ways that `foldl`
and `foldr` apply the given function to a list:
```agda
example-l : (x y z w : â„•) â†’ foldl max w (x :: y :: z :: []) â‰¡ max (max (max w z) y) x
example-l x y z w = refl _

example-r : (x y z w : â„•) â†’ foldr max w (x :: y :: z :: []) â‰¡ max x (max y (max z w))
example-r x y z w = refl _
```

**Task 4.1.** A function `f : A â†’ A â†’ A` is said to be commutative if `f x y â‰¡ f y x`. Define this in Agda.
```agda
is-commutative : {X : Type} (f : X â†’ X â†’ X) â†’ Type
is-commutative {X} f = (x y : X) â†’ f x y â‰¡ f y x
```

**Task 4.2.** Show that, if the given function `f` is commutative, then `foldr` and
`foldl` give the same result:
```agda
commutative-implies-foldr-coincides-with-foldl
 : {X : Type}
 â†’ (f : X â†’ X â†’ X)
 â†’ is-commutative f
 â†’ (x : X) (xs : List X)
 â†’ foldr f x xs â‰¡ foldl f x xs
commutative-implies-foldr-coincides-with-foldl f fcomm u [] = refl u
commutative-implies-foldr-coincides-with-foldl f fcomm u (x :: xs) =
 f x (foldr f u xs) â‰¡âŸ¨ fcomm x (foldr f u xs) âŸ©
 f (foldr f u xs) x â‰¡âŸ¨ ap (Î» a â†’ f a x) (commutative-implies-foldr-coincides-with-foldl f fcomm u xs) âŸ©
 f (foldl f u xs) x âˆ
```

## Question 5

A **permutation** is defined as an isomorphism of the type `Fin n` with itself.

```agda
Permutation : â„• â†’ Type
Permutation n = Fin n â‰… Fin n
```

In this question, we are going to define the `sign` of such a
permutation.  To do so, first recall that the type `Fin n` carries a
natural total order, and that this order is decidable:

```agda
_<[Fin]_ : {n : â„•} â†’ Fin n â†’ Fin n â†’ Type
zero <[Fin] zero = ğŸ˜
zero <[Fin] suc q = ğŸ™
suc p <[Fin] zero = ğŸ˜
suc p <[Fin] suc q = p <[Fin] q

<[Fin]-decidable : {n : â„•} â†’ (p : Fin n) â†’ (q : Fin n) â†’ is-decidable (p <[Fin] q)
<[Fin]-decidable zero zero = ğŸ˜-is-decidable
<[Fin]-decidable zero (suc _) = ğŸ™-is-decidable
<[Fin]-decidable (suc _) zero = ğŸ˜-is-decidable
<[Fin]-decidable (suc p) (suc q) = <[Fin]-decidable p q
```

With this in mind, the `sign` of a permutation can be defined as the
*parity of the number of pairs which are transposed with respect to
this order*.

Here is an example with `n = 5`, with the permutation `f` indicated as a table for convenience.
```notagda
    i | 0 1 2 3 4
  ----+----------
  f i | 4 2 1 3 0
```
Now consider, looking at the bottom row of the above table, the following pairs:
```notagda
   (4 , 2) (4 , 1) (4 , 3) (4 , 0) -- We have 4 transpositions.
           (2 , 1) (2 , 3) (2 , 0) -- We have 2 transpositions.
                   (1 , 3) (1 , 0) -- We have 1 transposition.
                           (3 , 0) -- We have 1 transposition.
```
These are the pairs `(f i , f j)` with `i < j`. Such a pair is a called a transposition if `Â¬(f i < f j)`.
We indicate the transpositions with square brackets:
```notagda
   [4 , 2] [4 , 1] [4 , 3] [4 , 0]
           [2 , 1] (2 , 3) [2 , 0]
                   (1 , 3) [1 , 0]
                           [3 , 0]
```
So in this example there are only two pairs which are not transposed.
Because the number of transposed pairs is `8`, which is even, the parity is `ğŸ : ğŸš`.

**Task.** Define a function that computes the sign of a permutation.
```
flip : ğŸš â†’ ğŸš
flip ğŸ = ğŸ
flip ğŸ = ğŸ

enumFin : (n : â„•) â†’ List (Fin n)
enumFin zero    = []
enumFin (suc n) = zero :: map suc (enumFin n)

cartesian : {X Y : Type} â†’ List X â†’ List Y â†’ List (X Ã— Y)
cartesian []        ys = []
cartesian (x :: xs) ys = map (x ,_) ys ++ cartesian xs ys

enumFinÂ² : (n : â„•) â†’ List (Fin n Ã— Fin n)
enumFinÂ² n = cartesian (enumFin n) (enumFin n)

flipForTransposed : {n : â„•} â†’ Permutation n â†’ List (Fin n Ã— Fin n) â†’ ğŸš â†’ ğŸš
flipForTransposed Ïƒ [] b              = b
flipForTransposed Ïƒ ((p , q) :: ps) b = âˆ”-nondep-elim
 (Î» p<q â†’ âˆ”-nondep-elim
           (Î» Ïƒp<Ïƒq â†’ flipForTransposed Ïƒ ps b)
           (Î» Ïƒp>Ïƒq â†’ flip (flipForTransposed Ïƒ ps b))
           (<[Fin]-decidable (Ïƒ .bijection p) (Ïƒ .bijection q)))
 (Î» p>q â†’ flipForTransposed Ïƒ ps b)
 (<[Fin]-decidable p q)

sign : (n : â„•) â†’ Permutation n â†’ ğŸš
sign n Ïƒ = go (enumFinÂ² n)
  where go : List (Fin n Ã— Fin n) â†’ ğŸš
        go [] = ğŸ
        go ((l , r) :: xs) with <[Fin]-decidable l r | <[Fin]-decidable (bijection Ïƒ l) (bijection Ïƒ r)
        ... | inl x | inl y = go xs
        ... | inl x | inr y = flip (go xs)
        ... | inr x | inl y = go xs
        ... | inr x | inr y = go xs
```

The above example of a permutation can be coded in Agda as follows, and you
may wish to use it for testing. Notice that f is its own inverse.
```agda
ğ•— : Permutation 5
ğ•— = Isomorphism f (Inverse f ff ff)
 where
  f : Fin 5 â†’ Fin 5
  f zero = suc (suc (suc (suc zero)))
  f (suc zero) = suc (suc zero)
  f (suc (suc zero)) = suc zero
  f (suc (suc (suc zero))) = suc (suc (suc zero))
  f (suc (suc (suc (suc zero)))) = zero

  ff : f âˆ˜ f âˆ¼ id
  ff zero = refl _
  ff (suc zero) = refl _
  ff (suc (suc zero)) = refl _
  ff (suc (suc (suc zero))) = refl _
  ff (suc (suc (suc (suc zero)))) = refl _
```

**Hint** : Try to enumerate the pairs of elements of `Fin n` and count the number of transpositions.
