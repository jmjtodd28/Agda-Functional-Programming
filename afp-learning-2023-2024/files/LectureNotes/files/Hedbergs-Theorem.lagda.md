# Hedberg's Theorem

This is rather advanced material.

Sometimes we wish to know that the identity type `x â‰¡ y` has at most one element for all elements `x` and `y` of a type. In this case we say that the type is a set. Alternatively, one says that the type satisfies *uniqueness of identity proofs* (UIP), or that it satisfies the *K axiom*. Perhaps surprisingly, this can't be proved for all types. But it can be proved for quite a few types, including the booleans, natural numbers, and functions `â„• â†’ â„•`, among many others.

Hedberg's Theorem, whose proof is short, but quite difficult to understand, even for experts in Martin-LÃ¶f type theory, is a main tool for establishing that some types are sets.
Agda has the axiom `K` discussed above enabled by default. We are disabling it in all modules, including this. The reason is that towards the end of term we intend to give examples of types that are not sets, and explain why they are interesting.

```agda
{-# OPTIONS --without-K --safe #-}

module Hedbergs-Theorem where

open import prelude
open import negation

has-at-most-one-element : Type â†’ Type
has-at-most-one-element X = (x y : X) â†’ x â‰¡ y

ğŸ˜-has-at-most-one-element : has-at-most-one-element ğŸ˜
ğŸ˜-has-at-most-one-element () ()

ğŸ™-has-at-most-one-element : has-at-most-one-element ğŸ™
ğŸ™-has-at-most-one-element â‹† â‹† = refl â‹†

is-set : Type â†’ Type
is-set X = (x y : X) â†’ has-at-most-one-element (x â‰¡ y)

is-constant : {X Y : Type} â†’ (X â†’ Y) â†’ Type
is-constant {X} f = (x x' : X) â†’ f x â‰¡ f x'

has-constant-endofunction : Type â†’ Type
has-constant-endofunction X = Î£ f ê‰ (X â†’ X), is-constant f

_âˆ™_ : {A : Type} {x y z : A} â†’ x â‰¡ y â†’ y â‰¡ z â†’ x â‰¡ z
p âˆ™ q = trans p q

_â»Â¹ : {A : Type} {x y : A} â†’ x â‰¡ y â†’ y â‰¡ x
p â»Â¹ = sym p

infix  40 _â»Â¹

â»Â¹-leftâˆ™ : {X : Type} {x y : X} (p : x â‰¡ y)
         â†’ p â»Â¹ âˆ™ p â‰¡ refl y
â»Â¹-leftâˆ™ (refl x) = refl (refl x)

â»Â¹-rightâˆ™ : {X : Type} {x y : X} (p : x â‰¡ y)
          â†’ p âˆ™ p â»Â¹ â‰¡ refl x
â»Â¹-rightâˆ™ (refl x) = refl (refl x)

Hedbergs-Lemma : {X : Type} (x : X)
               â†’ ((y : X) â†’ has-constant-endofunction (x â‰¡ y))
               â†’ (y : X) â†’ has-at-most-one-element (x â‰¡ y)
Hedbergs-Lemma {X} x c y p q = II
 where
  f : (y : X) â†’ x â‰¡ y â†’ x â‰¡ y
  f y = fst (c y)

  Îº : (y : X) (p q : x â‰¡ y) â†’ f y p â‰¡ f y q
  Îº y = snd (c y)

  I : (y : X) (p : x â‰¡ y) â†’ (f x (refl x))â»Â¹ âˆ™ f y p â‰¡ p
  I x (refl x) = r
   where
    r : (f x (refl x)) â»Â¹ âˆ™ f x (refl x) â‰¡ refl x
    r = â»Â¹-leftâˆ™ (f x (refl x))

  II = p                         â‰¡âŸ¨ (I y p)â»Â¹                          âŸ©
       (f x (refl x))â»Â¹ âˆ™ f y p  â‰¡âŸ¨ ap ((f x (refl x))â»Â¹ âˆ™_) (Îº y p q) âŸ©
       (f x (refl x))â»Â¹ âˆ™ f y q  â‰¡âŸ¨ I y q                              âŸ©
       q                         âˆ


is-Hedberg-type : Type â†’ Type
is-Hedberg-type X = (x y : X) â†’ has-constant-endofunction (x â‰¡ y)

Hedberg-types-are-sets : (X : Type)
                       â†’ is-Hedberg-type X
                       â†’ is-set X
Hedberg-types-are-sets X c x = Hedbergs-Lemma x
                                (Î» y â†’ fst (c x y) , snd (c x y))

pointed-types-have-constant-endofunction : {X : Type}
                                         â†’ X
                                         â†’ has-constant-endofunction X
pointed-types-have-constant-endofunction x = ((Î» y â†’ x) , (Î» y y' â†’ refl x))

empty-types-have-constant-endofunction : {X : Type}
                                       â†’ is-empty X
                                       â†’ has-constant-endofunction X
empty-types-have-constant-endofunction e = (id , (Î» x x' â†’ ğŸ˜-elim (e x)))

open import decidability

decidable-types-have-constant-endofunctions : {X : Type}
                                            â†’ is-decidable X
                                            â†’ has-constant-endofunction X
decidable-types-have-constant-endofunctions (inl x) =
 pointed-types-have-constant-endofunction x
decidable-types-have-constant-endofunctions (inr e) =
 empty-types-have-constant-endofunction e

types-with-decidable-equality-are-Hedberg : {X : Type}
                                          â†’ has-decidable-equality X
                                          â†’ is-Hedberg-type X
types-with-decidable-equality-are-Hedberg d x y =
 decidable-types-have-constant-endofunctions (d x y)

Hedbergs-Theorem : {X : Type} â†’ has-decidable-equality X â†’ is-set X
Hedbergs-Theorem {X} d = Hedberg-types-are-sets X
                          (types-with-decidable-equality-are-Hedberg d)

â„•-is-set : is-set â„•
â„•-is-set = Hedbergs-Theorem â„•-has-decidable-equality

Bool-is-set : is-set Bool
Bool-is-set = Hedbergs-Theorem Bool-has-decidable-equality
```
